buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.0.3'
    }
}

apply plugin: 'java'
apply plugin: 'application'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

group               = 'testgrp'
version             = '0.1.0-SNAPSHOT'
status              = 'integration'
description         = 'TODO - Pls. edit - Gradle bootstrap project for Comsat'

ext.containerPort   = 8080

ext.classifier      = ':jdk8' // ''
sourceCompatibility = 1.8     // 1.7
targetCompatibility = 1.8     // 1.7

ext.h2Ver           = '1.4.185'
ext.dropwizardVer   = '0.7.1'
ext.daggerVer       = '1.2.2'
ext.jerseyVer       = '2.14'
ext.feignVer        = '7.2.1'
ext.servletVer      = '3.0.1'
ext.comsatVer       = '0.3.0'
ext.quasarVer       = '0.6.2'
ext.junitVer        = '4.12'

ext.guavaVer        = '18.0'
ext.jacksonVer      = '2.3.3'

configurations {
    quasar
    provided
}

sourceSets {
    main.compileClasspath = main.compileClasspath + configurations.provided
    test.compileClasspath = test.compileClasspath + configurations.provided
    test.runtimeClasspath = test.runtimeClasspath + configurations.provided
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        force "com.google.guava:guava:$guavaVer"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.h2database:h2:$h2Ver"

    compile "com.squareup.dagger:dagger:$daggerVer"
    provided "com.squareup.dagger:dagger-compiler:$daggerVer"
    
    compile "co.paralleluniverse:comsat-jdbi:$comsatVer"
    compile ("co.paralleluniverse:comsat-dropwizard:$comsatVer") {
        exclude group: 'com.sun.jersey', module: '*'
    }
    compile "io.dropwizard:dropwizard-core:$dropwizardVer";
    compile "co.paralleluniverse:quasar-core:$quasarVer"
    compile "co.paralleluniverse:comsat-jdbc:$comsatVer"

    provided "javax.servlet:javax.servlet-api:$servletVer"

    quasar "co.paralleluniverse:quasar-core:${quasarVer}${classifier}@jar"

    testCompile "junit:junit:$junitVer"
}

mainClassName = "${group}.dw.Main"
applicationDefaultJvmArgs = [
    "-javaagent:${configurations.quasar.singleFile}",
    "-Dco.paralleluniverse.fibers.verifyInstrumentation=true",
    "-Ddropwizard.config=build/resources/main/conf.yml"
]
